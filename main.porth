include "std.porth"

macro BF_SLOT_CAP 1024 30 * end

// memory layout
macro STDIN_BUF mem end // each time the program encounters a ',' op, this value increases by one
macro PUTC_BUF STDIN_BUF 8 + end // used to print value of cell as an ascii char
macro OP_STACK PUTC_BUF 8 + end // pointer to the operation stack
macro OP_COUNT OP_STACK ,64 end // number of OP's on the OP_STACK
macro OP_STACK_END OP_STACK 8 + OP_COUNT + end // pointer to last OP
macro BF_DATA_PTR OP_STACK_END 1 + end // pointer to the data pointer
macro BF_CELLS BF_DATA_PTR 8 + end // pointer to first cell
macro BF_WHILE_JMP BF_CELLS BF_SLOT_CAP + 1 + end // stores how many OP's to jump back if condition is not null

// operations
macro OP_INC_PTR 1 end // >
macro OP_DEC_PTR 2 end // <
macro OP_INC     3 end // +
macro OP_DEC     5 end // -
macro OP_OUT     6 end // .
macro OP_IN      7 end // ,
macro OP_WHILE   8 end // [
macro OP_DO      9 end // ]

// print one character to stdout
macro putc 
    if PUTC_BUF , 0 != do
        "PORTHDUCK ERROR: corrupted stack\n" eputs 1 exit
    end
    PUTC_BUF swap .64
    1 PUTC_BUF puts
    PUTC_BUF 0 .64
end

// push one operator to OP_STACK
macro push_op
    if OP_COUNT BF_SLOT_CAP >= do
        "PORTHDUCK ERROR: stack overflow.\n" eputs 1 exit
    end
    OP_STACK_END 1 + swap .
    OP_STACK inc64
end

macro parse_bf
    while dup , 0 != do
        if dup , '>' = do
            OP_INC_PTR push_op
        else if dup , '<' = do
            OP_DEC_PTR push_op
        else if dup , '+' = do
            OP_INC push_op
        else if dup , '-' = do
            OP_DEC push_op
        else if dup , '.' = do
            OP_OUT push_op
        else if dup , ',' = do
            OP_IN push_op
        else if dup , '[' = do
            OP_WHILE push_op
        else if dup , ']' = do
            OP_DO push_op
        end end end end end end end end
        1 +
    end
    drop drop
end

macro sim_bf
    1
    while dup OP_COUNT <= do
        dup 8 + OP_STACK + ,

        if dup OP_INC_PTR = do
            BF_DATA_PTR inc64
        else if dup OP_DEC_PTR = do
            BF_DATA_PTR dec64
        else if dup OP_INC = do
            BF_CELLS BF_DATA_PTR ,64 + dup , 1 + .
        else if dup OP_DEC = do
            BF_CELLS BF_DATA_PTR ,64 + dup , 1 - .
        else if dup OP_OUT = do
            BF_CELLS BF_DATA_PTR ,64 + , putc
        else if dup OP_IN = do
            if STDIN_BUF ,64 2 nth_argv strlen 1 + >= do
                "PORTHDUCK INFO: program tried accessing non-existent input\n" puts
                BF_CELLS BF_DATA_PTR ,64 + 0 .
            else
                BF_CELLS BF_DATA_PTR ,64 +
                2 nth_argv STDIN_BUF ,64 + , .
            end
            STDIN_BUF inc64
        else if dup OP_WHILE = do
            BF_WHILE_JMP 1 + BF_WHILE_JMP , .
            swap BF_WHILE_JMP over . swap
        else if dup OP_DO = do
            if BF_CELLS BF_DATA_PTR , + , 0 != do
                swap drop BF_WHILE_JMP ,64 swap
            else
                BF_WHILE_JMP BF_WHILE_JMP 1 + , .
            end
        end end end end end end end end
        drop
        1 +
    end
    drop
end

macro main
    1 nth_argv dup strlen swap

    parse_bf
    "PORTHDUCK INFO: finished parsing\n" puts
    sim_bf
end

main